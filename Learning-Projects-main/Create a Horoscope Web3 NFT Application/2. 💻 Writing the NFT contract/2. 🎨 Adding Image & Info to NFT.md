# 💻 Writing the NFT contract

## **🎨 Adding Image & Info to NFT**

Every NFT has some data linked to it in the form of JSON format that describes the NFT. We call it metadata. This metadata has a special JSON format and the format needs to be followed if we want our NFT to appear properly on platforms like OpenSea, Rarible, etc.

In the previous lesson, I shared how we wrote ‘zodiacSign’ instead of sharing the Uniform Resource Identifier, I mean, the data about the NFT. 😅

Let’s look back at it 👀

```
_setTokenURI(newItemId, "zodiacSign");
```

Now we will use the standard format required by NFT platforms to describe the data related to the NFT. The following is the JSON structure, we are mentioning name of Image, Description, and attributes related to Image. With Image, you can give a link to externally hosted image, or just create an SVG structure.

```
{
     "name":"Virgo",
     "description":"Perfectionist. Great Educators. Overthinkers.",
     "image":"your-hosted-img-url.jpg or svg",
      "attributes":
      [
         { "trait_type":"Zodiac","value":"Virgo"
         },
         { "trait_type":"Month","value":"August"
         }
     ]
 }
```

For this project, I am creating a basic black box of SVG, that has the zodiac name mentioned in the center. I’d urge you to be creative and come up with something exciting.

```
<svg width="350" height="350" xmlns="http://www.w3.org/2000/svg" preserveAspectRatio="xMinYMin meet">
 <g>
  <title>Layer 1</title>
  <rect id="svg_1" fill="black" height="100%" width="100%"/>
  <text id="svg_2" font-size="24px" font-family="serif" fill="white" text-anchor="middle" dominant-baseline="middle" class="base" y="50%" x="50%">Virgo</text>
 </g>
</svg>
```

This is my svg structure, I will dynamically replace Virgo with the calculated Zodiac sign in future. For now lets finalize our metadata and then run this smart contract.

I will go back to my horoscopeNFT contract file and first I will make my svg code dynamic so that the zodiac sign name can be added to the image in run time. I will declare string type baseSvg and add the first part of my svg. This line will go right after the counters declaration.

```
string baseSvg ="<svg xmlns='http://www.w3.org/2000/svg' preserveAspectRatio='xMinYMin meet' viewBox='0 0 350 350'><style>.base { fill: white; font-family: serif; font-size: 24px; }</style><rect width='100%' height='100%' fill='black' /><text x='50%' y='50%' class='base' dominant-baseline='middle' text-anchor='middle'>";
```

Next, I will open the mintNFT function and do the following.

```
function mintNFT(address recipient, string memory zodiacSign)
   public
   returns (uint256)
{
   _tokenIds.increment();
 
   string memory finalSvg = string(
       abi.encodePacked(baseSvg, zodiacSign, "</text></svg>")
   );
 
   // Get all the JSON metadata in place and base64 encode it.
   string memory json = Base64.encode(
       bytes(
           string(
               abi.encodePacked(
                   '{"name": "',zodiacSign,
                   // We set the title of our NFT as the generated word.
                   '", "description": "On-chain Zodiac Sign NFTs", "attributes": [{"trait_type": "Zodiac Sign", "value": "',
                   zodiacSign,
                   '"}], "image": "data:image/svg+xml;base64,',
                   // We add data:image/svg+xml;base64 and then append our base64 encode our svg.
                   Base64.encode(bytes(finalSvg)),
                   '"}'
               )
           )
       )
   );
 
   // Just like before, we prepend data:application/json;base64, to our data.
   string memory finalTokenUri = string(
       abi.encodePacked("data:application/json;base64,", json)
   );
 
   uint256 newItemId = _tokenIds.current();
   _mint(recipient, newItemId);
   _setTokenURI(newItemId, finalTokenUri);
 
   return newItemId;
}
```

Let me explain the code step by step.

```
string memory finalSvg = string(
       abi.encodePacked(baseSvg, zodiacSign, "</text></svg>")
   );
```

Here we are just concatenating the baseSvg, the zodiacSign and end tags. Then we wrap it in string just to make sure that our final outcome is string type.

```
// Get all the JSON metadata in place and base64 encode it.
    string memory json = Base64.encode(
        bytes(
            string(
                abi.encodePacked(
                    '{"name": "',zodiacSign,
                    // We set the title of our NFT as the generated word.
                    '", "description": "On-chain Zodiac Sign NFTs", "attributes": [{"trait_type": "Zodiac Sign", "value": "',
                    zodiacSign,
                    '"}], "image": "data:image/svg+xml;base64,',
                    // We add data:image/svg+xml;base64 and then append our base64 encode our svg.
                    Base64.encode(bytes(finalSvg)),
                    '"}'
                )
            )
        )
    );
```

We create our json structure and encode it in base64. Similarly, we populate finalTokenURI with json structure, encode it in base64 and declare the format to be json.

```
// Just like before, we prepend data:application/json;base64, to our data.
    string memory finalTokenUri = string(
        abi.encodePacked("data:application/json;base64,", json)
    );
```

Awesome! Phew!! The hardest part is over. This is how the final contract looks like.

```
//SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.1;
 
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Counters.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "./Base64.sol";
 
contract horoscopeNFT is ERC721URIStorage {
    using Counters for Counters.Counter;
    Counters.Counter private _tokenIds;
 
string baseSvg =
    "<svg xmlns='http://www.w3.org/2000/svg' preserveAspectRatio='xMinYMin meet' viewBox='0 0 350 350'><style>.base { fill: white; font-family: serif; font-size: 24px; }</style><rect width='100%' height='100%' fill='black' /><text x='50%' y='50%' class='base' dominant-baseline='middle' text-anchor='middle'>";
 
constructor() ERC721("Horoscope NFT", "HNFT") {}
 
function mintNFT(address recipient, string memory zodiacSign)
    public
    returns (uint256)
{
    _tokenIds.increment();
 
    string memory finalSvg = string(
        abi.encodePacked(baseSvg, zodiacSign, "</text></svg>")
    );
 
    // Get all the JSON metadata in place and base64 encode it.
    string memory json = Base64.encode(
        bytes(
            string(
                abi.encodePacked(
                    '{"name": "',zodiacSign,
                    // We set the title of our NFT as the generated word.
                    '", "description": "On-chain Zodiac Sign NFTs", "attributes": [{"trait_type": "Zodiac Sign", "value": "',
                    zodiacSign,
                    '"}], "image": "data:image/svg+xml;base64,',
                    // We add data:image/svg+xml;base64 and then append our base64 encode our svg.
                    Base64.encode(bytes(finalSvg)),
                    '"}'
                )
            )
        )
    );
 
    // Just like before, we prepend data:application/json;base64, to our data.
    string memory finalTokenUri = string(
        abi.encodePacked("data:application/json;base64,", json)
    );
 
    uint256 newItemId = _tokenIds.current();
    _mint(recipient, newItemId);
    _setTokenURI(newItemId, finalTokenUri);
 
    return newItemId;
    }
}
```

Since we are encoding our json and svg in Base64, we will need to import that file too. Here is the  [link to download](https://github.com/Metaschoolso/Learning-Projects/blob/main/Base64.sol)  that file and add to your contracts folder.

Our function mintNFT is dynamic now, it takes the address and zodiac sign and then mints an NFT to the account.

Now we will prepare for deployment.

### Assignment

#### Any feedback for us to improve metaschool?

Any feedback for us to improve metaschool? We would love to hear it.

**Your response is**


> Written with [StackEdit](https://stackedit.io/).
